plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id "com.gorylenko.gradle-git-properties" version "2.4.0-rc2"
    id "com.github.ben-manes.versions" version "0.40.0"
}


apply plugin: 'java'
apply plugin: 'idea'

version = '0.10.18'
group = 'com.avairebot'
description = 'AvaIre Discord Bot'
mainClassName = 'com.avairebot.Main'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    moduleName = 'AvaIre'
}

shadowJar {
    archiveName = "AvaIre.jar"

    doLast {
        copy {
            from 'build/libs/AvaIre.jar'
            into '.'
        }
    }
}

tasks.withType(JavaCompile) {
    dependsOn 'clean'
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:-unchecked"
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'AvaIre: Discord Bot',
            'Implementation-Version': version,
            'Main-Class': 'com.avairebot.Main'
    }

    baseName = project.name

    from { configurations.implementation.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    failOnError = false
    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.author()
    options.encoding = 'UTF-8'
    options.addStringOption('-html5')
}

repositories {
    mavenCentral()                     // Main maven repo
    jcenter()                          // JDA and some other stuff
    mavenLocal()                       // Useful for developing
    maven { url "https://jitpack.io" } // Build projects directly from github

    // Used for pulling down JDA and LavaPlayer
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
}

dependencies {
    implementation group: 'com.avairebot', name: 'Shared', version: '80809049d2'
    implementation group: 'net.dv8tion', name: 'JDA', version: '4.2.1_256'
    implementation group: 'com.sedmelluq', name: 'lavaplayer', version: '1.3.75'
    implementation group: 'com.sedmelluq', name: 'lavaplayer-ext-youtube-rotator', version: '0.2.3'
    implementation group: 'com.sedmelluq', name: 'jda-nas', version: '1.1.0'
    implementation group: 'com.github.FredBoat', name: 'Lavalink-Client', version: '4.0'

    implementation group: 'commons-cli', name: 'commons-cli', version: '1.3.1'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.1'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.1'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.19'
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.11.2'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
    implementation group: 'mysql', name: 'mysql-connector-java', version: '5.1.46'
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.21.0.1'
    implementation group: 'ai.api', name: 'libai', version: '1.6.12'

    implementation group: 'com.sparkjava', name: 'spark-core', version: '2.7.1'
    implementation group: 'io.prometheus', name: 'simpleclient', version: '0.1.0'
    implementation group: 'io.prometheus', name: 'simpleclient_servlet', version: '0.1.0'
    implementation group: 'io.prometheus', name: 'simpleclient_logback', version: '0.1.0'
    implementation group: 'io.prometheus', name: 'simpleclient_hotspot', version: '0.1.0'
    implementation group: 'io.prometheus', name: 'simpleclient_guava', version: '0.1.0'

    implementation group: 'com.udojava', name: 'EvalEx', version: '1.18'
    implementation group: 'com.vdurmont', name: 'emoji-java', version: '4.0.0'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    implementation group: 'io.sentry', name: 'sentry-logback', version: '1.7.10'
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.11'

    // Tests
    // Only needed to run tests in a version of IntelliJ IDEA that bundles older versions
    implementation("org.junit.platform:junit-platform-launcher:1.6.2")
    implementation("org.junit.jupiter:junit-jupiter-engine:5.6.2")
    implementation("org.junit.vintage:junit-vintage-engine:5.6.2")
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filesMatching("**/app.properties") {
        filter ReplaceTokens, tokens: [
            "project.version"   : project.version,
            "project.groupId"   : project.group,
            "project.artifactId": project.ext.moduleName
        ]
    }
}
